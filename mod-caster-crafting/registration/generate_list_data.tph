/***************************************************************************************************
*   This file will be the first thing that the mod does. It will skim through any registered data
*   --presently: in this mod; future: an API to let others register data in weidu_external--
/   for the spells, scrolls, poitions, and wands. It's like pre-processing.
***************************************************************************************************/

/*********************************************
*   Set up mod install detection variables   *
*********************************************/
OUTER_SET ~bz_iwdification_installed~ = 0
ACTION_IF (MOD_IS_INSTALLED ~SETUP-IWDIFICATION.TP2~ ~30~)
    BEGIN
        OUTER_SET ~bz_iwdification_installed~ = 1
    END

OUTER_SET ~bz_OlvynSpells_installed~ = 0
//1000 = powerful spells; 1001 = no powerful spells
ACTION_IF (MOD_IS_INSTALLED ~MESpells.tp2~ ~1000~) OR (MOD_IS_INSTALLED ~MESpells.tp2~ ~1001~)
    BEGIN
        OUTER_SET ~bz_OlvynSpells_installed~ = 1

        OUTER_SET ~bz_olvyn_powerful~ = (MOD_IS_INSTALLED ~MESpells.tp2~ ~1000~)
        OUTER_SET ~bz_olvyn_eeex~ =
            MOD_IS_INSTALLED ~EEEX.TP2~ ~0~
            AND
            (
                INSTALL_ORDER ~MESpells.tp2~ ~1000~ AFTER ~EEEX.TP2~ ~0~
                OR INSTALL_ORDER ~MESpells.tp2~ ~1001~ AFTER ~EEEX.TP2~ ~0~
            )
        OUTER_SET ~bz_olvyn_powerful_eeex~ =
            MOD_IS_INSTALLED ~EEEX.TP2~ ~0~
            AND INSTALL_ORDER ~MESpells.tp2~ ~1000~ AFTER ~EEEX.TP2~ ~0~

        OUTER_SET ~bz_olvyn_wi556~ =    //Restore original Animate Dead for wizards?
            !(MOD_IS_INSTALLED ~MESpells.tp2~ ~25011~) AND !(MOD_IS_INSTALLED ~MESpells.tp2~ ~25012~)

        OUTER_SET ~bz_olvyn_true_strike~ =    //Replace Infravision with True Strike?
            (MOD_IS_INSTALLED ~MESpells.tp2~ ~21111~)

        //Debugging Statements
        PRINT ~DEBUG: Olvyn:
            powerful:           `%bz_olvyn_powerful%`;
            EEex:               `%bz_olvyn_eeex%`;
            Powerful + EEex:    `%bz_olvyn_powerful_eeex%`;
            Skeleton Horde:     `%bz_olvyn_wi556%`;~
    END



/*********************
*   Include arrays   *
*********************/
//Include function defs.
INCLUDE ~%MOD_FOLDER%/registration/fn_lookup_costs_copy_spell.tph~
INCLUDE ~%MOD_FOLDER%/registration/fn_lookup_costs_scroll.tph~
INCLUDE ~%MOD_FOLDER%/registration/fn_calculate_scroll_cost.tph~

//Include spell lists
INCLUDE ~%MOD_FOLDER%/registration/spell-lists/%bz_game_code%/core/arcane.tph~
INCLUDE ~%MOD_FOLDER%/registration/spell-lists/%bz_game_code%/core/priest.tph~

ACTION_IF (%bz_iwdification_installed%)
    BEGIN
        PRINT ~DEBUG: Include IWD-ification Spells~
        INCLUDE ~%MOD_FOLDER%/registration/spell-lists/%bz_game_code%/iwdification/arcane.tph~
        INCLUDE ~%MOD_FOLDER%/registration/spell-lists/%bz_game_code%/iwdification/priest.tph~
    END

ACTION_IF (%bz_OlvynSpells_installed%)
    BEGIN
        PRINT ~DEBUG: Include Olvyn Spells~
        INCLUDE ~%MOD_FOLDER%/registration/spell-lists/%bz_game_code%/olvyn-spells/arcane.tph~
        INCLUDE ~%MOD_FOLDER%/registration/spell-lists/%bz_game_code%/olvyn-spells/priest.tph~
    END

//Include lookup tables
INCLUDE ~%MOD_FOLDER%/registration/tables/%bz_edition_code%/costs-copy-spell.tph~



//OUTPUT:
/*
    An associative array named "spell_list_data" what contains these keys:
        [sorting Name]          // 0    --get from level and spell file/TLK
        [Spell RESREF]          // 1
        [Tradition]             // 2
        [Spell Level]           // 3
        [Gold Cost]             // 4    --lookup from table
        [Time Cost]             // 5    hours -- lookup from table
        [Spell Display Name]    // 6    --get from spell
        [Exclusion Flags]       // 7    --get from spell file
*/
INCLUDE ~%MOD_FOLDER%/registration/generate_spell_list_data.tph~
