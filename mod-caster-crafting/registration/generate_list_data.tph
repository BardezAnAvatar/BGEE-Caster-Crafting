/***************************************************************************************************
*   This file will be the first thing that the mod does. It will skim through any registered data
*   --presently: in this mod; future: an API to let others register data in weidu_external--
/   for the spells, scrolls, poitions, and wands. It's like pre-processing.
***************************************************************************************************/

/*********************************************
*   Set up mod install detection variables   *
*********************************************/
OUTER_SET ~bz_iwdification_installed~ = 0
ACTION_IF (MOD_IS_INSTALLED ~SETUP-IWDIFICATION.TP2~ ~30~)
    BEGIN
        OUTER_SET ~bz_iwdification_installed~ = 1
    END

OUTER_SET ~bz_OlvynSpells_installed~ = 0
OUTER_SET ~bz_iwdification_installed_after_OlvynSpells~ = 0
//1000 = powerful spells; 1001 = no powerful spells
ACTION_IF (MOD_IS_INSTALLED ~MESpells.tp2~ ~1000~) OR (MOD_IS_INSTALLED ~MESpells.tp2~ ~1001~)
    BEGIN
        OUTER_SET ~bz_OlvynSpells_installed~ = 1

        OUTER_SET ~bz_olvyn_powerful~ = (MOD_IS_INSTALLED ~MESpells.tp2~ ~1000~)
        OUTER_SET ~bz_olvyn_eeex~ =
            MOD_IS_INSTALLED ~EEEX.TP2~ ~0~
            AND
            (
                INSTALL_ORDER ~MESpells.tp2~ ~1000~ AFTER ~EEEX.TP2~ ~0~
                OR INSTALL_ORDER ~MESpells.tp2~ ~1001~ AFTER ~EEEX.TP2~ ~0~
            )
        OUTER_SET ~bz_olvyn_powerful_eeex~ =
            MOD_IS_INSTALLED ~EEEX.TP2~ ~0~
            AND INSTALL_ORDER ~MESpells.tp2~ ~1000~ AFTER ~EEEX.TP2~ ~0~

        OUTER_SET ~bz_olvyn_wi556~ =    //Restore original Animate Dead for wizards?
            !(MOD_IS_INSTALLED ~MESpells.tp2~ ~25011~) AND !(MOD_IS_INSTALLED ~MESpells.tp2~ ~25012~)

        OUTER_SET ~bz_olvyn_true_strike~ =    //Replace Infravision with True Strike?
            (MOD_IS_INSTALLED ~MESpells.tp2~ ~21111~)

        //Debugging Statements
        PRINT ~DEBUG: Olvyn:
            powerful:           `%bz_olvyn_powerful%`;
            EEex:               `%bz_olvyn_eeex%`;
            Powerful + EEex:    `%bz_olvyn_powerful_eeex%`;
            Skeleton Horde:     `%bz_olvyn_wi556%`;~
    END

ACTION_IF (bz_iwdification_installed AND bz_OlvynSpells_installed) THEN
BEGIN
    OUTER_SET ~bz_iwdification_installed_after_OlvynSpells~ =
    (
        INSTALL_ORDER ~SETUP-IWDIFICATION.TP2~ ~1000~ AFTER ~MESpells.tp2~ ~1000~
        OR INSTALL_ORDER ~SETUP-IWDIFICATION.TP2~ ~1001~ AFTER ~MESpells.tp2~ ~1001~
    )
END


PRINT ~
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Detecting installed spells   !
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
~


/*********************
*   Include arrays   *
*********************/
//Include function defs.
INCLUDE ~./%MOD_FOLDER%/registration/fn_lookup_costs_copy_spell.tph~
INCLUDE ~./%MOD_FOLDER%/registration/fn-bz_lookup_costs_scroll.tph~
INCLUDE ~./%MOD_FOLDER%/registration/fn-bz_calculate_scroll_cost.tph~

//Include spell lists
INCLUDE ~./%MOD_FOLDER%/registration/spell-lists/%bz_game_code%/core/arcane.tph~
INCLUDE ~./%MOD_FOLDER%/registration/spell-lists/%bz_game_code%/core/innate.tph~
INCLUDE ~./%MOD_FOLDER%/registration/spell-lists/%bz_game_code%/core/priest.tph~

ACTION_IF (%bz_OlvynSpells_installed%)
    BEGIN
        PRINT ~DEBUG: Include Olvyn Spells~
        INCLUDE ~./%MOD_FOLDER%/registration/spell-lists/%bz_game_code%/olvyn-spells/arcane.tph~
        INCLUDE ~./%MOD_FOLDER%/registration/spell-lists/%bz_game_code%/olvyn-spells/priest.tph~
    END

//Create the `SpellResRefLookup` Associative array, key => RESREF; DO THIS BEFORE EXPANDING INTO IWDification
INCLUDE ~./%MOD_FOLDER%/registration/generate-Spell_ResRef_Lookup.tph~

ACTION_IF (%bz_iwdification_installed%)
    BEGIN
        PRINT ~DEBUG: Include IWD-ification Spells~
        INCLUDE ~./%MOD_FOLDER%/registration/spell-lists/%bz_game_code%/iwdification/arcane.tph~
        INCLUDE ~./%MOD_FOLDER%/registration/spell-lists/%bz_game_code%/iwdification/priest.tph~
    END

//Append to the `SpellResRefLookup` Associative array, key (IDS entry) => RESREF
INCLUDE ~./%MOD_FOLDER%/registration/generate-Spell_ResRef_Lookup-IWD.tph~


//Include lookup tables
INCLUDE ~./%MOD_FOLDER%/registration/tables/%bz_edition_code%/costs-copy-spell.tph~
//OUTPUT:
/*
    An associative array named "SpellResRefLookup" that contains teh following keys:
        [Key]           // 0    RESREF for base game and olvynspells; IDS script name for IWD
            =>
        [RESREF]        // value RESREF that exists in game
*/

/* DEBUG */
/*
PRINT ~DEBUG: SpellResRefLookup:~
ACTION_PHP_EACH ~SpellResRefLookup~
    AS ~keys~ => ~value~
    BEGIN
        //Debugging Statements
        PRINT ~-Spell Lookup Record:
            Unique Key:     `%keys_0%`
            Value:          `%value%`~
    END
*/



//OUTPUT:
/*
    An associative array named "spell_list_data" what contains these keys:
        [sorting Name]          // 0    --get from level and spell file/TLK
        [Spell RESREF]          // 1
        [Tradition]             // 2
        [Spell Level]           // 3
        [Gold Cost]             // 4    --lookup from table
        [Time Cost]             // 5    hours -- lookup from table
        [Spell Display Name]    // 6    --get from spell
        [Exclusion Flags]       // 7    --get from spell file


    An associative array named "spell_lookup" what contains these keys:
        [Spell RESREF]          // 0
        [Spell Display Name]    // value    --get from spell/TLK
*/
INCLUDE ~./%MOD_FOLDER%/registration/generate_spell_list_data.tph~
