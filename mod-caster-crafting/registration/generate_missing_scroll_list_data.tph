//RESULT:
/*
    An associative array named "scroll_list_data_arcane" what contains these keys:
        [Scroll]                // 0
        [Spell]                 // 1
        [Tradition]             // 2
        [Spell Level]           // 3
        [Caster Level]          // 4
        [Gold Cost]             // 5    -- function to calculate
        [Icon: Inventory]       // 6
        [Usability: Mage]       // 7
        [Usability: Cleric]     // 8
        [Usability: Druid]      // 9
        [Usability: Bard]       // 10
        [Usability: Paladin]    // 11
        [Usability: Ranger]     // 12
*/

ACTION_PHP_EACH ~scroll_list_missing~
    AS ~keys~ => ~value~
    BEGIN
        //calculate the gold cost
        LAUNCH_ACTION_FUNCTION ~bz_calculate_scroll_cost~
            INT_VAR
                ~CasterLevel~ = %keys_4%
                ~AdditionalCost~ = %keys_6%
            STR_VAR
                ~Level~ = ~%keys_3%~
                ~Tradition~ = ~%keys_2%~
            RET
                ~scroll_gold~ = ~gold~
        END


        ACTION_DEFINE_ASSOCIATIVE_ARRAY
            ~scroll_list_missing_data_arcane~
            BEGIN
                ~%keys_0%~,                 //scroll
                ~%keys_1%~,                 //spell
                ~%keys_2%~,                 //tradition
                ~%keys_3%~,                 //spell level
                ~%keys_4%~,                 //caster level
                ~%scroll_gold%~,            //cost of the scroll in the *.ITM file
                ~%keys_5%~,                 //inventory icon
                ~%keys_7%~,                 //Usability: Mage
                ~%keys_8%~,                 //Usability: Cleric
                ~%keys_9%~,                 //Usability: Druid
                ~%keys_10%~,                //Usability: Bard
                ~%keys_11%~,                //Usability: Paladin
                ~%keys_12%~                 //Usability: Ranger
                    => ~%keys_0%~           //a rather arbitrary value to pick. could be name, scroll, etc.
            END

    END

//Sort the indicies, on first key
ACTION_SORT_ARRAY_INDICES ~scroll_list_missing_data_arcane~

//DEBUG

ACTION_PHP_EACH ~scroll_list_missing_data_arcane~
    AS ~keys~ => ~value~
    BEGIN
        //Debugging Statements
        PRINT ~DEBUG: scroll_list_data_arcane:
            spell code:     `%keys_1%`
            scroll code:    `%keys_0%`
            Tradition:      `%keys_2%`
            level:          `%keys_3%`
            caster level:   `%keys_4%`
            gold cost:      `%keys_5%`
            inv. icon:      `%keys_6%`
            us. Mage:       `%keys_7%`
            us. Cleric:     `%keys_8%`
            us. Druid:      `%keys_9%`
            us. Bard:       `%keys_10%`
            us. Paladin:    `%keys_11%`
            us. Ranger:     `%keys_12%`~
    END


