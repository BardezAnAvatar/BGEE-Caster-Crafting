//RESULT:
/*
    An associative array named "scroll_list_data" what contains these keys:
    [Scroll]                    // 0
    [Spell]                     // 1
    [Tradition]                 // 2
    [Spell Level]               // 3
    [Caster Level]              // 4
    [Gold Cost]                 // 5
    [Icon: Source BAM Path]     // 6
    [Icon: Inventory]           // 7
    [Usability: Mage]           // 8
    [Usability: Cleric]         // 9
    [Usability: Druid]          // 10
    [Usability: Bard]           // 11
    [Usability: Paladin]        // 12
    [Usability: Ranger]         // 13
    [Usability: Shaman]         // 14
    [Usability: Good]           // 15
    [Usability: ... Neutral]    // 16
    [Usability: Evil]           // 17
    [Usability: Lawful]         // 18
    [Usability: Neutral ...]    // 19
    [Usability: Chaotic]        // 20
*/

ACTION_PHP_EACH ~scroll_list_missing~
    AS ~keys~ => ~value~
    BEGIN
        //calculate the gold cost
        LAUNCH_ACTION_FUNCTION ~bz_calculate_scroll_cost~
            INT_VAR
                ~CasterLevel~ = %keys_4%
                ~AdditionalCost~ = %keys_5%
            STR_VAR
                ~Level~ = ~%keys_3%~
                ~Tradition~ = ~%keys_2%~
            RET
                ~scroll_gold~ = ~gold~
        END


        ACTION_DEFINE_ASSOCIATIVE_ARRAY
            ~scroll_list_missing_data~
            BEGIN
                ~%keys_0%~,                 //scroll
                ~%keys_1%~,                 //spell
                ~%keys_2%~,                 //tradition
                ~%keys_3%~,                 //spell level
                ~%keys_4%~,                 //caster level
                ~%scroll_gold%~,            //cost of the scroll in the *.ITM file
                ~%keys_6%~,                 //Source BAM Path
                ~%keys_7%~,                 //inventory icon
                ~%keys_8%~,                 //Usability: Mage
                ~%keys_9%~,                 //Usability: Cleric
                ~%keys_10%~,                //Usability: Druid
                ~%keys_11%~,                //Usability: Bard
                ~%keys_12%~,                //Usability: Paladin
                ~%keys_13%~,                //Usability: Ranger
                ~%keys_14%~,                //Usability: Shaman
                ~%keys_15%~,                //Usability: Good
                ~%keys_16%~,                //Usability: ... Neutral
                ~%keys_17%~,                //Usability: Evil
                ~%keys_18%~,                //Usability: Lawful
                ~%keys_19%~,                //Usability: Neutral ...
                ~%keys_20%~,                //Usability: Chaotic
                    => ~%keys_0%~           //a rather arbitrary value to pick. could be name, scroll, etc.
            END

    END

//Sort the indicies, on first key
ACTION_SORT_ARRAY_INDICES ~scroll_list_missing_data~

//DEBUG

ACTION_PHP_EACH ~scroll_list_missing_data~
    AS ~keys~ => ~value~
    BEGIN
        //Debugging Statements
        PRINT ~DEBUG: scroll_list_missing_data:
            spell code:         `%keys_1%`
            scroll code:        `%keys_0%`
            Tradition:          `%keys_2%`
            level:              `%keys_3%`
            .spl header level:  `%keys_4%`
            gold cost:          `%keys_5%`
            BAM Path:           `%keys_6%`
            inv. icon:          `%keys_7%`
            usability, Mage:    `%keys_8%`
            usability, Cleric:  `%keys_9%`
            usability, Druid:   `%keys_10%`
            usability, Bard:    `%keys_11%`
            usability, Paladin: `%keys_12%`
            usability, Ranger:  `%keys_13%`
            usability, Shaman:  `%keys_14%`
            usability, Good:    `%keys_15%`
            usability, Neutral: `%keys_16%`
            usability, Evil:    `%keys_17%`
            usability, Lawful:  `%keys_18%`
            usability, Neutral: `%keys_19%`
            usability, Chaotic: `%keys_20%`~
    END


