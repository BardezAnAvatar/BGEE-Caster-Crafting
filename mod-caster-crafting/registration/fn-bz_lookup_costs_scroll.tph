DEFINE_ACTION_FUNCTION ~fn-bz_lookup_costs_scroll~
    STR_VAR
        ~Level~ = ~~
        ~Tradition~ = ~~
    RET
        ~time~
        ~xp~
        ~failure~
        ~time_engine~
        ~time_readable~
    BEGIN
        OUTER_SPRINT ~time~ ~~
        OUTER_SPRINT ~time_engine~ ~~
        OUTER_SPRINT ~time_readable~ ~~
        OUTER_SPRINT ~xp~ ~~
        OUTER_SPRINT ~failure~ ~~

        ACTION_PHP_EACH ~scroll_costs~
            AS ~costs~ => ~_~
        BEGIN
            //if the tradition and spell level match...
            ACTION_IF ((~%costs_2%~ STRING_EQUAL ~%Tradition%~) AND (~%costs_1%~ STRING_EQUAL ~%Level%~))
            THEN
            BEGIN
                OUTER_SPRINT ~time~ ~%costs_3%~
                OUTER_SPRINT ~xp~ ~%costs_4%~
                OUTER_SPRINT ~failure~ ~%costs_5%~
            END
        END

        //calculate a friendly time for display on dialog (hours vs. days)
        ACTION_IF %time% < 24 THEN
        BEGIN
            OUTER_SET ~hours~ = %time%
            OUTER_SPRINT ~time_readable~ @15034 // `%keys_5% hours`
        END
        ELSE    //days
        BEGIN
            OUTER_SET ~days~ = %time% / 24
            OUTER_SPRINT ~time_readable~ @15033 // `(%keys_5% / 24) days`
        END

        //calculate the in-engine time in engine clock ticks
        OUTER_SET ~time_engine~ = %time% * 300
    END
