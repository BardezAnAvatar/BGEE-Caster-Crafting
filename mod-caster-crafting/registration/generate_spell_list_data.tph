//OUTPUT:
/*
    An associative array named "spell_list_data" what contains these keys:
        [sorting Name]          // 0    --get from level and spell file/TLK
        [Spell RESREF]          // 1
        [Tradition]             // 2
        [Spell Level]           // 3
        [Gold Cost]             // 4    --lookup from table
        [Time Cost]             // 5    hours -- calc from table
        [Spell Display Name]    // 6    --get from spell/TLK
        [Exclusion Flags]       // 7    --get from spell file

    An associative array named "spell_lookup" what contains these keys:
        [Spell RESREF]          // 0
        [Spell Display Name]    // value    --get from spell/TLK
*/

//includes
INCLUDE ~./%MOD_FOLDER%/registration/fn-lookup_spell_resref.tph~


ACTION_PHP_EACH ~spell_list~
    AS ~keys~ => ~value~
    BEGIN

        //lookup the spell's ResRef from the identifier in the array
        LAUNCH_ACTION_FUNCTION ~fn-lookup_spell_resref~ STR_VAR ~identifier~ = ~%keys_0%~ RET ~spell_resref~ = ~resref~ END


        COPY_EXISTING - ~%spell_resref%.spl~ ~override~ //inline only, just to read from spell
            READ_LONG NAME1 ~strref_name~               //since this is a spell file, NAME1 should be all there is
            READ_LONG 0x1E ~exclusion_flags~            //read all of the kit exclusion flags
        BUT_ONLY_IF_IT_CHANGES

        ACTION_GET_STRREF ~strref_name~ ~spell_name~    //convert the read STRREF to an actual string

        //Get the costs from the table

        //you cannot copy divine spells, make it 0
        ACTION_IF (~%keys_1%~ STRING_EQUAL "Divine") THEN
        BEGIN
            OUTER_SET ~copy_cost_gold~ = 0
            OUTER_SET ~actual_copy_cost_gold~ = 0
        END
        ELSE    //arcane
        BEGIN
            LAUNCH_ACTION_FUNCTION ~bz_lookup_costs_copy_spell~
                STR_VAR
                    ~Level~ = ~%keys_2%~
                RET
                    ~copy_cost_time~ = ~time~
                    ~copy_cost_gold~ = ~gold~
            END

            OUTER_SET ~actual_copy_cost_gold~ = (%copy_cost_gold% / 4)
        END

        ACTION_DEFINE_ASSOCIATIVE_ARRAY
            ~spell_list_data~
            BEGIN
                ~%keys_2%_%spell_name%~,    //sorting key of level and spell name
                ~%spell_resref%~,           //spell RESREF
                ~%keys_1%~,                 //tradition
                ~%keys_2%~,                 //spell level
                ~%actual_copy_cost_gold%~,  //cost to copy the spell
                ~%copy_cost_time%~,         //time to scribe (in hours)
                ~%spell_name%~,             //spell name for display
                ~%exclusion_flags%~         //LONG bit field for kit exclusion. SEE: Kitlist.2da, but I'mma hard code this.
                    => ~%spell_resref%~     //a rather arbitrary value to pick. could be name, spell code, etc.
            END

        ACTION_DEFINE_ASSOCIATIVE_ARRAY
            ~spell_lookup~
            BEGIN
                ~%spell_resref%~            //spell RESREF
                    => ~%spell_name%~       //spell name
            END

    END

//Sort the indicies, presumably on first key
ACTION_SORT_ARRAY_INDICES ~spell_list_data~
