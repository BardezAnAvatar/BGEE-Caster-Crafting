/***************************************************************************
*   Calculates the cost in gold that should be assigned to a scroll item   *
***************************************************************************/
//includes
INCLUDE ~./%MOD_FOLDER%/registration/fn-lookup_spell_min_caster_level.tph~

DEFINE_ACTION_FUNCTION ~fn-bz_calculate_scroll_cost~
    INT_VAR
        ~CasterLevel~ = 0
        ~AdditionalCost~ = 0
    STR_VAR
        ~Level~ = ~~
        ~Tradition~ = ~~
    RET
        ~gold~
    BEGIN
        OUTER_SET ~gold~ = 0

        //lookup the minimum caster level
        LAUNCH_ACTION_FUNCTION ~fn-lookup_spell_min_caster_level~
            STR_VAR
                Tradition = null
                Level = null
            RET
                ~min_caster_level~ = ~minimum_caster_level~
        END

        //lookup the base spell cost by level
        OUTER_SET ~base_gold~ = 0
        ACTION_PHP_EACH ~costs_scroll_item~
            AS ~keys~ => ~_~
                BEGIN
                    //if the spell level matches...
                    ACTION_IF ((~%keys_1%~ STRING_EQUAL ~%Tradition%~) AND (~%keys_2%~ STRING_EQUAL ~%Level%~))
                    THEN
                    BEGIN
                        OUTER_SET ~base_gold~ = %keys_3%
                    END
                END

        //get excess caster level
        OUTER_SET ~excess_caster_level~ = 0
        ACTION_IF (%CasterLevel% > %min_caster_level%) THEN
        BEGIN
            OUTER_SET ~excess_caster_level~ = (%CasterLevel% - %min_caster_level%)
        END

        //get level
        OUTER_SET ~spell_level~ = 0
        ACTION_IF (~A~ STRING_EQUAL ~%Level%~) THEN BEGIN OUTER_SET ~spell_level~ = 10 END
            ELSE BEGIN ACTION_IF (~Q~ STRING_EQUAL ~%Level%~) THEN BEGIN OUTER_SET ~spell_level~ = 8 END
            ELSE BEGIN OUTER_SET ~spell_level~ = %Level% END
        END

        //calculate the ultimate gold cost
        OUTER_SET ~gold~ = (%base_gold% + %AdditionalCost% + (%excess_caster_level% * 50 * %spell_level%))

    END
