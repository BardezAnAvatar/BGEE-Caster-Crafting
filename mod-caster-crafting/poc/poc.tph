/***********
* research *
***********/

//NOTE: Confirmed that the array sorts as I expected it to.

//debug script
COMPILE ~./%MOD_FOLDER%/poc/bz-debug.baf~

// Now attempt to... what? make a dialog file?

//level 1 spells
APPEND_OUTER
    ~./%MOD_FOLDER%/poc/bzcrftar.d~
    ~~~~~


EXTEND_TOP ~BZCRFTAR~ bz_caster_craft_arcane_circle_1
    ~~~~~

ACTION_PHP_EACH ~scroll_list_data~
    AS ~scroll_list_data_keys~ => ~value~
    BEGIN
        ACTION_IF scroll_list_data_keys_4 STRING_EQUAL ~1~  //Spell is level 1
            THEN
            BEGIN
                APPEND_OUTER
                    ~./%MOD_FOLDER%/poc/bzcrftar.d~
                    ~~~~~
    IF ~HaveKnownSpellRES("%scroll_list_data_keys_2%")~
    THEN
        REPLY ~%scroll_list_data_keys_11%~
        GOTO bz_caster_craft_arcane_%scroll_list_data_keys_2%
                    ~~~~~
            END
    END

//end scroll state links
APPEND_OUTER
    ~./%MOD_FOLDER%/poc/bzcrftar.d~
    ~~~~~
END
    ~~~~~

//New section for all of the scroll states
APPEND_OUTER
    ~./%MOD_FOLDER%/poc/bzcrftar.d~
    ~~~~~
APPEND ~BZCRFTAR~
    ~~~~~


//Create spell state and responses
ACTION_PHP_EACH ~scroll_list_data~
    AS ~scroll_list_data_keys~ => ~value~
    BEGIN

        OUTER_SET scroll_total_cost = (%scroll_list_data_keys_6% + %scroll_list_data_keys_7%)
        OUTER_SET bz_craft_time = %scroll_list_data_keys_8% * 300  //I really wanted to call the variable "timepants"

        ACTION_IF scroll_list_data_keys_4 STRING_EQUAL ~1~  //Spell is level 1
            THEN
            BEGIN

                OUTER_SPRINT ~state_spell_entry~ @10050     //~You look over the spell %scroll_list_data_keys_11% and inspect its entry in your spellbook.~
                OUTER_SPRINT ~reply_scroll_create~ @10033   //~[Scribe a scroll of %scroll_list_data_keys_11%. Gather materials costing %scroll_total_cost% gold, and spend %scroll_list_data_keys_8% hours scribing the scroll.~

                APPEND_OUTER
                    ~./%MOD_FOLDER%/poc/bzcrftar.d~
                    ~~~~~
    IF ~~
    THEN BEGIN bz_caster_craft_arcane_%scroll_list_data_keys_2%
        SAY ~%state_spell_entry%~ //~You look over the spell %scroll_list_data_keys_11% and inspect its entry in your spellbook.~
            IF ~~
            THEN
                REPLY ~%reply_scroll_create%~ //~[Scribe a scroll of %scroll_list_data_keys_11%. Gather materials costing %scroll_total_cost% gold, and spend %scroll_list_data_keys_8% hours scribing the scroll.~
                DO
                    ~
TakePartyGold(%scroll_total_cost%)
DestroyGold(%scroll_total_cost%)
AdvanceTime(%bz_craft_time%)
GiveItemCreate("%scroll_list_data_keys_1%", Myself, 1, 0, 0)
                    ~
                EXIT
            IF ~~
            THEN REPLY @10032 //~[Pick another spell]~
                GOTO bz_caster_craft_arcane_circle_%scroll_list_data_keys_4%
            IF ~~
            THEN REPLY @10031 //~[Pick another spell circle]~
                GOTO bz_caster_craft_arcane_qualified
            IF ~~
            THEN REPLY @10030 //~[Close your spellbook and pack up your scribe tools]~
                EXIT
    END
                    ~~~~~
            END
    END

APPEND_OUTER
    ~./%MOD_FOLDER%/poc/bzcrftar.d~
    ~~~~~
END
    ~~~~~

COMPILE ~./%MOD_FOLDER%/poc/bzcrftar.d~

//NOTE: The dialog now compiles, no warnings, and I can verify in NI that it should work.
//NOTE: The time of 1800 conflicts with some mod-custom timers in a weird way;
    // it should compile to SIX_HOURS, I guess, but instead compiles to LK#NEERA_TIMER (also 1800)

