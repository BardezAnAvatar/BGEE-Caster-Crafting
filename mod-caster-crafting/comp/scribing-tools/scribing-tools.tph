/************************
*   Scribing kit stuff  *
************************/

/******************************************************************************
*   NOTE: This file will expect prior actions to have altered
*   BZMSCST.D and yielded internal WeiDU strings:
*       - SpellbookCopy/BZMSCST.D
*       - ScrollCreation/BZMSCST.D
******************************************************************************/

//Emit the final result prior to compilation
COPY -
    ~./%MOD_FOLDER%/comp/scribing-tools/DLG/BZMSCST.d~
    ~Generated/BZMSCST.d~
        //Patches
        APPEND_FILE TEXT ~ScrollCreation/BZMSCST.D~
        APPEND_FILE TEXT ~SpellbookCopy/BZMSCST.D~

//DEBUG so I can read it
COPY
    ~Generated/BZMSCST.d~
    ~./%MOD_FOLDER%/comp/scribing-tools/DLG/BZMSCST.DEBUG.d~

//compile the new dialog (with huge mutated edits)
COMPILE ~Generated/BZMSCST.d~


/****************************
*   Make the scribing kit   *
****************************/
//Copy BAMs and shit for item
COPY ~./%MOD_FOLDER%/comp/scribing-tools/BAM/BZMISTD.BAM~ ~override~    //arcane kit description BAM
COPY ~./%MOD_FOLDER%/comp/scribing-tools/BAM/BZMISTI.BAM~ ~override~    //arcane kit inventory BAM


//Make an item for the player to interact with
ACTION_IF (~%bz_game_code%~ STRING_EQUAL ~pstee~)
THEN
BEGIN
    COPY_EXISTING ~DBOX.ITM~ ~override/BZMSCST.ITM~   //scribe kit
        //Patches
            SAY NAME1 @20000 //Scribe's Tool Kit
            SAY NAME2 @20000 //Scribe's Tool Kit
            SAY UNIDENTIFIED_DESC @20002
            SAY IDENTIFIED_DESC @20001
            WRITE_ASCII 0x3A "BZMISTI" (8)                  // Icon
            WRITE_LONG 0x34 2000                            // Price
            WRITE_SHORT 0x38 1                              // Stack Size
            WRITE_LONG 0x42 10                              // Lore to Identify
            WRITE_LONG 0x4C 10                              // Weight
            READ_LONG 0x18 ~bzmsckar_flags~
            WRITE_LONG 0x18 (%bzmsckar_flags% BOR BIT11)    //set conversible
            WRITE_ASCII 0x58 "Z_CLOTH1" (8)                 // Pick up Sound
            WRITE_ASCII 0x10 "Z_CLOTH2" (8)                 // Drop Sound
            WRITE_LONG 0x1C 0                               // Category: Miscellaneous
            WRITE_LONG 0x1E
                BIT31 BOR BIT29 BOR BIT28 BOR BIT26 BOR BIT25 BOR
                BIT23 BOR BIT22 BOR BIT21 BOR BIT17 BOR BIT11 // unusability flags: Not non-mages
END
ELSE
BEGIN
    COPY_EXISTING ~MISC01.ITM~ ~override/BZMSCST.ITM~   //scribe kit
        //Patches
            SAY NAME1 @20000 //Scribe's Tool Kit
            SAY NAME2 @20000 //Scribe's Tool Kit
            SAY UNIDENTIFIED_DESC @20002
            SAY IDENTIFIED_DESC @20001
            WRITE_ASCII 0x3A "BZMISTI" (8)                  // Icon
            WRITE_LONG 0x34 2000                            // Price
            WRITE_SHORT 0x38 1                              // Stack Size
            WRITE_SHORT 0x42 10                             // Lore to Identify
            WRITE_LONG 0x4C 10                              // Weight
            WRITE_ASCII 0x58 "BZMISTD" (8)                  // Description Image
            READ_LONG 0x18 ~bzmsckar_flags~
            WRITE_LONG 0x18 (%bzmsckar_flags% BOR BIT11)    //set conversible
END



//Register the item in ITEMDIAL.2DA along with the strref to talk to it.
OUTER_SET bz_scrollkit_arcane_interact_strref = RESOLVE_STR_REF(@20003)

COPY_EXISTING ~ITEMDIAL.2DA~ ~override~
    //PATCHES
        COUNT_2DA_ROWS 3 ~bz_itemdial_ct~
        INSERT_2DA_ROW %bz_itemdial_ct% 3 ~BZMSCST %bz_scrollkit_arcane_interact_strref% BZMSCST~

//Add the kit to stores
INCLUDE ~./%MOD_FOLDER%/comp/scribing-tools/add-to-stores.tph~

//Install the custscene
INCLUDE ~./%MOD_FOLDER%/comp/scribing-tools/install_cutscene.tph~
