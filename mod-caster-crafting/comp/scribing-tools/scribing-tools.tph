/************************
*   Scribing kit stuff  *
************************/

/******************************************************************************
*   NOTE: This file will expect prior components to have altered 
*   BZMSCST.D and yielded internal WeiDU strings:
*       - SpellbookCopy/BZMSCST.D
*       - ScrollCreation/BZMSCST.D
******************************************************************************/

//Emit the final result prior to compilation
COPY + //no need to back up a .D file
    ~./%MOD_FOLDER%/comp/scribing-tools/DLG/BZMSCST.d~
    ~./%MOD_FOLDER%/comp/scribing-tools/DLG/BZMSCST.d~
        //Patches
        APPEND_FILE TEXT ~SpellbookCopy/BZMSCST.D~
        APPEND_FILE TEXT ~ScrollCreation/BZMSCST.D~

//compile the new dialog (with huge mutated edits)
COMPILE ~./%MOD_FOLDER%/comp/scribing-tools/DLG/BZMSCST.d~


/****************************
*   Make the scribing kit   *
****************************/
//Copy BAMs and shit for item
COPY ~./%MOD_FOLDER%/comp/scribing-tools/BAM/BZMISTD.BAM~ ~override~    //arcane kit description BAM
COPY ~./%MOD_FOLDER%/comp/scribing-tools/BAM/BZMISTI.BAM~ ~override~    //arcane kit inventory BAM

//Make an item for the player to interact with
COPY_EXISTING ~MISC48.ITM~ ~override/BZMSCST.ITM~   //arcane kit
    //Patches
        SAY NAME1 @20000 //Scrollmaker's Work Kit
        SAY NAME2 @20000 //Scrollmaker's Work Kit
        SAY UNIDENTIFIED_DESC @20002
        SAY IDENTIFIED_DESC @20001
        WRITE_ASCII_TERMINATE 0x3A "BZMISTI"            // Icon
        WRITE_LONG 0x34 2000                            // Price
        WRITE_LONG 0x42 10                              // Lore to Identify
        WRITE_LONG 0x4C 10                              // Weight
        WRITE_ASCII_TERMINATE 0x58 "BZMISTD"            // Description Image
        READ_LONG 0x18 ~bzmsckar_flags~
        WRITE_LONG 0x18 (%bzmsckar_flags% BOR BIT11)    //set conversible


//Register the item in ITEMDIAL.2DA along with the strref to talk to it.
OUTER_SET bz_scrollkit_arcane_interact_strref = RESOLVE_STR_REF(@20003)

COPY_EXISTING ~ITEMDIAL.2DA~ ~override~
    //PATCHES
        COUNT_2DA_ROWS 3 ~bz_itemdial_ct~
        INSERT_2DA_ROW %bz_itemdial_ct% 3 ~BZMSCST %bz_scrollkit_arcane_interact_strref% BZMSCST~
