DEFINE_ACTION_FUNCTION ~bz_generate_potion_category_transitions~
    STR_VAR
        ~category~ = ~~ //lower-cast category name to add transitions to
    BEGIN

        OUTER_SPRINT ~category_lower~ ~%category%~
        ACTION_TO_LOWER ~category_lower~

        APPEND_OUTER -
            ~Potions/BZMSCAK.D~
            ~~~~~

EXTEND_TOP ~BZMSCAK~
    ~bz-potion-%category_lower%~ //state number(s)
            ~~~~~

        //isolate the unique potion entries; specific potions, ignoring spells and thus de-duplicating
        ACTION_PHP_EACH ~potion_definitions~
            AS ~keys~ => ~value~
            BEGIN
                ACTION_IF (~%keys_2%~ STRING_EQUAL ~%category%~)
                THEN
                BEGIN

                    OUTER_SPRINT ~transition_response~ ~%keys_4%~

                    //Give the potion a dialog option
                    APPEND_OUTER -
                        ~Potions/BZMSCAK.D~
                        ~~~~~
        IF ~~
        THEN
            REPLY ~%transition_response%~
            GOTO ~bz-potion-%category_lower%-%value%~
                        ~~~~~
                END //comparison
            END //iterate through categories

APPEND_OUTER -
    ~Potions/BZMSCAK.D~
    ~~~~~
END
    ~~~~~

APPEND_OUTER -
    ~Potions/BZMSCAK.D~
    ~~~~~

EXTEND_TOP ~BZMSCAK~
    ~bz-potion-main_menu~ //state number(s)
        IF ~~
        THEN
            REPLY ~%category%~
            GOTO ~bz-potion-%category_lower%~
END

    ~~~~~

    END //function
