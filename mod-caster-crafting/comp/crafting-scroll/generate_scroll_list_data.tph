//Look up the scroll names from the scroll ITM file to retrieve the TLK strings, so they can sort by language.
//iterate through the scroll list to generate the scroll-list data, then sort that data below

//RESULT:
/*
    An associative array named "scroll_list_data" what contains these keys:
        [sorting Name]          // 0    --get from spell
        [Scroll]                // 1
        [Spell]                 // 2
        [Tradition]             // 3
        [Spell Level]           // 4
        [Caster Level]          // 5
        [Gold Cost]             // 6    --get from scroll
        [Additional Cost]       // 7
        [Time Cost]             // 8    hours -- lookup from table
        [XP Cost]               // 9    --lookup from table
        [Failure %]             // 10   --lookup from table
        [Spell Display Name]    // 11   --get from spell
*/

//includes
INCLUDE ~./%MOD_FOLDER%/registration/fn-lookup_spell_resref.tph~

ACTION_PHP_EACH ~scroll_list~
    AS ~keys~ => ~value~
    BEGIN

        //lookup the spell's ResRef from the identifier in the array
        LAUNCH_ACTION_FUNCTION ~fn-lookup_spell_resref~ STR_VAR ~identifier~ = ~%keys_1%~ RET ~spell_resref~ = ~resref~ END

        OUTER_TEXT_SPRINT ~spell_name~ $spell_lookup("%spell_resref%")

        COPY_EXISTING - ~%keys_0%.itm~ ~override~   //inline only, just to read from scroll
            READ_LONG 0x34 ~scroll_price~           //read the price
        BUT_ONLY_IF_IT_CHANGES

        //Get the costs from the table
        LAUNCH_ACTION_FUNCTION ~fn-bz_lookup_costs_scroll~
            STR_VAR
                ~Level~ = ~%keys_3%~
                ~Tradition~ = ~%keys_2%~
            RET
                ~scroll_cost_time~ = ~time~
                ~scroll_cost_xp~ = ~xp~
                ~scroll_cost_failure~ = ~failure~
        END

        ACTION_DEFINE_ASSOCIATIVE_ARRAY
            ~scroll_list_data~
            BEGIN
                ~%keys_2%-%keys_3%-1-%spell_name%~,    //sorting key of level and spell name
                ~%keys_0%~,                 //scroll
                ~%spell_resref%~,           //spell
                ~%keys_2%~,                 //tradition
                ~%keys_3%~,                 //spell level
                ~%keys_4%~,                 //caster level
                ~%scroll_price%~,           //cost of the scroll in the *.ITM file
                ~%keys_5%~,                 //any additional cost
                ~%scroll_cost_time%~,       //time to scribe (in hours)
                ~%scroll_cost_xp%~,         //XP cost to scribe
                ~%scroll_cost_failure%~,    //Chance of failure
                ~%spell_name%~              //spell name for display
                    => ~%keys_0%~           //a rather arbitrary value to pick. could be name, scroll, etc.
            END
    END

//Sort the indicies, presumably on first key
ACTION_SORT_ARRAY_INDICES ~scroll_list_data~
