

/*******************************
*   ###
*   Begin Copy Spells Stuff
*   ### 
*******************************/

//Copy spells response/transitions
APPEND_OUTER
    ~./%MOD_FOLDER%/comp/scrafting-scroll/BZMSCSC.d~
    ~~~~~
  
  
EXTEND_TOP ~BZMSCSC~ bz_caster_craft_scribe_copy_menu
    ~~~~~

//loop through all players
OUTER_FOR (i = 1; i < 7; ++i)
    BEGIN
        OUTER_TEXT_SPRINT ~PlayerN~ = ~PLAYER%i%~
        OUTER_TEXT_SPRINT ~tag_PlayerN~ = ~<%PlayerN%>~
        OUTER_TEXT_SPRINT ~var_PlayerN~ = ~player%i%~
        OUTER_SET ~i-minus-one~ = (%i% - 1)

        //Circles 1-6: Mages and Bards share it
        APPEND_OUTER
            ~./%MOD_FOLDER%/comp/scrafting-scroll/BZMSCSC.d~
            ~~~~~

            //%PlayerN%
            IF ~OR(2)
    Class(%PlayerN%, MAGE_ALL)
    Class(%PlayerN%, BARD_ALL)
!InPartySlot(MYSELF, %i-minus-one%)~
            THEN REPLY ~%tag_PlayerN%~ //whomever the player name is
                GOTO bz_caster_craft_scribe_copy_%var_PlayerN%
            ~~~~~
    END

//end copy state links
APPEND_OUTER
    ~./%MOD_FOLDER%/comp/scrafting-scroll/BZMSCSC.d~
    ~~~~~
END
    ~~~~~


//New states for each given player
APPEND_OUTER
    ~./%MOD_FOLDER%/comp/scrafting-scroll/BZMSCSC.d~
    ~~~~~
  
  
APPEND ~BZMSCSC~
    ~~~~~



//loop through all players
OUTER_FOR (i = 1; i < 7; ++i)
    BEGIN
        OUTER_TEXT_SPRINT ~PlayerN~ = ~PLAYER%i%~
        OUTER_TEXT_SPRINT ~tag_PlayerN~ = ~<%PlayerN%>~
        OUTER_TEXT_SPRINT ~var_PlayerN~ = ~player%i%~
        OUTER_SPRINT ~PlayerN-Prompt~ = @15071

        //Circles 1-6: Mages and Bards share it
        APPEND_OUTER
            ~./%MOD_FOLDER%/comp/scrafting-scroll/BZMSCSC.d~
            ~~~~~

    //%PlayerN%
    IF ~~ THEN BEGIN bz_caster_craft_scribe_copy_%var_PlayerN%
        SAY %PlayerN-Prompt%    //You study %tag_PlayerN%'s spellbook. Which spell circle do you explore?

            /* Circles 1-6: Mages and Bards */
            //Circle 1
            IF ~~
            THEN REPLY @15051 //1st arcane circle
                GOTO bz_caster_craft_scribe_copy_%var_PlayerN%_circle1

            //Circle 2
            IF ~~
            THEN REPLY @15052 //2nd arcane circle
                GOTO bz_caster_craft_scribe_copy_%var_PlayerN%_circle2

            //Circle 3
            IF ~~
            THEN REPLY @15053 //3rd arcane circle
                GOTO bz_caster_craft_scribe_copy_%var_PlayerN%_circle3

            //Circle 4
            IF ~~
            THEN REPLY @15054 //4th arcane circle
                GOTO bz_caster_craft_scribe_copy_%var_PlayerN%_circle4

            //Circle 5
            IF ~~
            THEN REPLY @15055 //5th arcane circle
                GOTO bz_caster_craft_scribe_copy_%var_PlayerN%_circle5

            //Circle 6
            IF ~~
            THEN REPLY @15056 //6th arcane circle
                GOTO bz_caster_craft_scribe_copy_%var_PlayerN%_circle6

            /* Circles 7-10: Mages only */

            //Circle 7
            IF ~Class(MYSELF, MAGE_ALL)
Class(%PlayerN%, MAGE_ALL)~
            THEN REPLY @15057 //7th arcane circle
                GOTO bz_caster_craft_scribe_copy_%var_PlayerN%_circle7

            //Circle 8
            IF ~Class(MYSELF, MAGE_ALL)
Class(%PlayerN%, MAGE_ALL)~
            THEN REPLY @15058 //8th arcane circle
                GOTO bz_caster_craft_scribe_copy_%var_PlayerN%_circle8

            //Circle 9
            IF ~Class(MYSELF, MAGE_ALL)
Class(%PlayerN%, MAGE_ALL)~
            THEN REPLY @15059 //9th arcane circle
                GOTO bz_caster_craft_scribe_copy_%var_PlayerN%_circle9

            //Circle 10
            IF ~Class(MYSELF, MAGE_ALL)
Class(%PlayerN%, MAGE_ALL)~
            THEN REPLY @15060 //10th arcane circle
                GOTO bz_caster_craft_scribe_copy_%var_PlayerN%_circle10

            IF ~~
            THEN REPLY @15072 //Look at someone else's spellbook.
                GOTO bz_caster_craft_scribe_copy_menu

            IF ~~
            THEN REPLY @15031 //Go back to your scribe tools.
                GOTO bz_caster_craft_scribe_main_menu

            IF ~~
            THEN REPLY @15030 //Pack up your scribe tools.
                EXIT

    END
            
            ~~~~~


            //Now I need to write all 10 circles to copy
            
    END


//end new states by player
APPEND_OUTER
    ~./%MOD_FOLDER%/comp/scrafting-scroll/BZMSCSC.d~
    ~~~~~
END
    ~~~~~        



/*******************************
*   ###
*   End Copy Spells Stuff
*   ### 
*******************************/

