/******************************************************
*   This script handles the Divine scroll menu        *
*      and dialog states, transitions, and crafting   *
******************************************************/

//Declare an empty file that I can use as a buffer
<<<<<<<< ScrollCreation/CirclesDivine.D

>>>>>>>>


/********************************************
*   Generate the transitions dynamicially   *
********************************************/

OUTER_FOR (i = 1; i < 9; ++i)
BEGIN
    //Get the appropriate spell circle variable set up
    OUTER_SPRINT ~divine_spell_circle~ ~0~
    ACTION_IF (i = 8) THEN BEGIN OUTER_SPRINT ~divine_spell_circle~ ~Q~ END
    ELSE BEGIN OUTER_SPRINT ~divine_spell_circle~ ~%i%~ END

    //Append to the buffer for each circle
    APPEND_OUTER -
        ~ScrollCreation/CirclesDivine.D~
        ~~~~~

EXTEND_TOP ~BZMSCST~
    bz_caster_craft_scribe_scroll_divine_circle_%divine_spell_circle% //state number(s)
        ~~~~~

    LAUNCH_ACTION_FUNCTION ~bz_lookup_costs_scroll~
        STR_VAR
            ~Level~ = ~%divine_spell_circle%~
            ~Tradition~ = ~Divine~
        RET
            ~bz_scroll_time~ = ~time~
            ~bz_scroll_failure~ = ~failure~
            ~scribe_time_engine~ = ~time_engine~
            ~readable_time~ = ~time_readable~
    END

    //Generate the transitions and name the states for individual divine scrolls
    ACTION_PHP_EACH ~scroll_list_data~
        AS ~keys~ => ~value~
        BEGIN
            //Get the cost in gold for the scroll
            COPY_EXISTING - ~%keys_2%.SPL~ ~override~
                //Patches
                READ_LONG (0x0C) ~bz_spell_name_STRREF~
            BUT_ONLY_IF_IT_CHANGES

            ACTION_GET_STRREF (%bz_spell_name_STRREF%) ~spell_name~

            OUTER_SPRINT ~divine_scroll_transition_response~ ~%spell_name%~

            //Give the scroll a dialog option
            APPEND_OUTER -
                ~ScrollCreation/CirclesDivine.D~
                ~~~~~
        IF ~HaveKnownSpellRES("%keys_2%")~
        THEN
            REPLY ~%divine_scroll_transition_response%~
            GOTO bz_caster_craft_scribe_scroll_divine_circle_%divine_spell_circle%_%keys_2%
                ~~~~~
        END

    //End the .D top-level command, `EXTEND_TOP`
    APPEND_OUTER -
        ~ScrollCreation/CirclesDivine.D~
        ~~~~~

END
        ~~~~~

END

/************************************************
*  END: Generate the transitions dynamicially   *
************************************************/




/*************************************************************
*   Generate Divine scroll states and transitions to craft   *
*************************************************************/
APPEND_OUTER -
    ~ScrollCreation/CirclesDivine.D~
    ~~~~~

APPEND ~BZMSCST~
    ~~~~~


OUTER_FOR (i = 1; i < 9; ++i)
BEGIN
    //Get the appropriate spell circle variable set up
    OUTER_SPRINT ~divine_spell_circle~ ~0~
    ACTION_IF (i = 8) THEN BEGIN OUTER_SPRINT ~divine_spell_circle~ ~Q~ END
    ELSE BEGIN OUTER_SPRINT ~divine_spell_circle~ ~%i%~ END

    LAUNCH_ACTION_FUNCTION ~bz_lookup_costs_scroll~
        STR_VAR
            ~Level~ = ~%divine_spell_circle%~
            ~Tradition~ = ~Divine~
        RET
            ~bz_scroll_time~ = ~time~
            ~bz_scroll_failure~ = ~failure~
            ~scribe_time_engine~ = ~time_engine~
            ~readable_time~ = ~time_readable~
    END

    //Calculate the bounds for failure
    OUTER_SET ~bz_scroll_failure_bounds_cursed~ = (99 - %bz_scroll_failure%)     //use this for GT(X)
    OUTER_SET ~bz_scroll_failure_bounds_success~ = (100 - %bz_scroll_failure%)   //use this for LT(X)

    //Generate the transitions and name the states for individual scrolls
    ACTION_PHP_EACH ~scroll_list_data~
        AS ~keys~ => ~value~
        BEGIN
            //Get the cost in gold for the scroll
            COPY_EXISTING - ~%keys_1%.ITM~ ~override~
                //Patches
                READ_LONG (0x34) ~scroll_gold_cost~
                READ_LONG NAME2 ~bz_scroll_name_STRREF~
            BUT_ONLY_IF_IT_CHANGES

            ACTION_GET_STRREF (%bz_scroll_name_STRREF%) ~scroll_name~
            OUTER_SPRINT ~spell_name~ ~%keys_11%~

            OUTER_SPRINT ~scroll_prompt~ @15090 // ~You meditate over the spell %spell_name% and prepare its power.~
            OUTER_SPRINT ~scroll_response~ @15035 // ~\[%scroll_gold_cost% gold; %readable_time%\] Scribe a scroll of %scroll_name%.~

            //Give the type of scroll as a dialog option
            APPEND_OUTER -
                ~ScrollCreation/CirclesDivine.D~
                ~~~~~
    IF ~~
    THEN BEGIN bz_caster_craft_scribe_scroll_divine_circle_%divine_spell_circle%_%keys_2%
        SAY ~%scroll_prompt%~
            IF ~HaveKnownSpellRES("%keys_2%")
RandomNumLT(100, %bz_scroll_failure_bounds_success%)~   //created successfully
            THEN
                REPLY ~%scroll_response%~
                DO ~
TakePartyGold(%scroll_gold_cost%)
DestroyGold(%scroll_gold_cost%)
AdvanceTime(%scribe_time_engine%)
GiveItemCreate("%value%", Myself, 1, 0, 0)
CreateVisualEffectObject("BZSCRIBE", Myself)
ClearAllActions()
StartCutSceneMode()
StartCutScene("BZCTWRIT")~
                EXIT
            IF ~HaveKnownSpellRES("%keys_2%")
RandomNumGT(100, %bz_scroll_failure_bounds_cursed%)~    //failure, create cursed scroll
            THEN
                REPLY ~%scroll_response%~
                DO ~
TakePartyGold(%scroll_gold_cost%)
DestroyGold(%scroll_gold_cost%)
AdvanceTime(%scribe_time_engine%)
CreateCreature("BZCSCRL", [-1.-1], S)
CreateVisualEffectObject("BZSCRIBE", Myself)
ActionOverride("BZ_CursedScrollGiver", GivePartyAllEquipment())
ActionOverride("BZ_CursedScrollGiver", DestroySelf())
ClearAllActions()
StartCutSceneMode()
StartCutScene("BZCTWRIT")~
                EXIT
            IF ~~
            THEN REPLY @15033 // ~Pick another spell~
                GOTO bz_caster_craft_scribe_scroll_divine_circle_%divine_spell_circle%
            IF ~~
            THEN REPLY @15032 // ~Pick another spell circle~
                GOTO bz_caster_craft_scribe_scroll_menu
            IF ~~
            THEN REPLY @15031 // ~Go back to your scribe tools.~
                GOTO bz_caster_craft_scribe_main_menu
            IF ~~
            THEN REPLY @15030 // ~Pack up your scribe tools.~
                EXIT
    END
                ~~~~~
        END

END


APPEND_OUTER -
    ~ScrollCreation/CirclesDivine.D~
    ~~~~~

END
    ~~~~~
/******************************************************************
*   END: Generate Divine scroll states and transitions to craft   *
******************************************************************/
