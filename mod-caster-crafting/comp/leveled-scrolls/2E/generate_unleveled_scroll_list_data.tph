/**********************************************************************
*   This function will iterate through the registered spell list,     *
*       scroll list and missing croll list, merging all the various   *
*       data together for one array that we can iterate over          *
*                                                                     *
*   Prerequisites:                                                    *
*       - ~spell_list~ is populated.                                  *
*       - ~scroll_list_missing~ is populated.                         *
*       - ~spell_min_caster_level~ is populated                       *
**********************************************************************/
//RESULT:
/*
    An associative array named "scroll_list_data" what contains these keys:
    [Spell]                             // 0
    [Tradition]                         // 1
    [Spell Level]                       // 2
    [Base Caster Level]                 // 3
    [Base Scroll Gold Cost]             // 4
    [Icon: Inventory BAM path/prefix]   // 5
*/

INCLUDE ~./%MOD_FOLDER%/registration/fn-bz_calculate_scroll_cost.tph~


//Include scroll BAM prefix lists
INCLUDE ~./%MOD_FOLDER%/registration/scroll-lists/%bz_game_code%/core/leveled_scroll_asset_prefixes.tph~

ACTION_IF (%bz_OlvynSpells_installed%)
BEGIN
    PRINT ~DEBUG: Include Olvyn Spells leveled scrolls~
    INCLUDE ~./%MOD_FOLDER%/registration/scroll-lists/%bz_game_code%/olvyn-spells/leveled_scroll_asset_prefixes.tph~
END

ACTION_IF (%bz_iwdification_installed%)
BEGIN
    PRINT ~DEBUG: Include IWD-ification leveled scrolls~
    INCLUDE ~./%MOD_FOLDER%/registration/scroll-lists/%bz_game_code%/iwdification/leveled_scroll_asset_prefixes.tph~
END





PRINT ~
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Generating array spell metadata for producing leveled scrolls   !
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
~


ACTION_PHP_EACH ~spell_list~
    AS ~keys~ => ~value~
BEGIN

    //clear/initialize variable
    OUTER_TEXT_SPRINT ~bz_unleveled_scroll_resref~ ""
    OUTER_TEXT_SPRINT ~bz_unleveled_scroll_base_gold~ "0"
    OUTER_TEXT_SPRINT ~bz_unleveled_scroll_base_caster_level~ "0"
    OUTER_TEXT_SPRINT ~bz_unleveled_scroll_spell_level~ "0"
    OUTER_TEXT_SPRINT ~bz_unleveled_scroll_bam_prefix~ ""

    ACTION_PHP_EACH ~scroll_list~
        AS ~scroll_keys~ => ~_v~
    BEGIN
        ACTION_IF (~%keys_0%~ STRING_EQUAL ~%scroll_keys_1%~) THEN
        BEGIN
            OUTER_TEXT_SPRINT ~bz_unleveled_scroll_resref~              ~%scroll_keys_0%~
            OUTER_TEXT_SPRINT ~bz_unleveled_scroll_base_caster_level~   ~%scroll_keys_4%~
            OUTER_TEXT_SPRINT ~bz_unleveled_scroll_spell_level~         ~%scroll_keys_3%~

            //get scroll cost
            COPY_EXISTING - ~%scroll_keys_0%.ITM~ ~override~
                READ_LONG (0x34) ~bz_unleveled_scroll_base_gold~
                BUT_ONLY
        END
    END

    ACTION_IF (~%bz_unleveled_scroll_resref%~ STRING_EQUAL ~~) THEN
    BEGIN
        ACTION_PHP_EACH ~scroll_list_missing~
            AS ~missing_scroll_keys~ => ~_v~
        BEGIN
            ACTION_IF (~%keys_0%~ STRING_EQUAL ~%missing_scroll_keys_1%~) THEN
            BEGIN
                OUTER_TEXT_SPRINT ~bz_unleveled_scroll_resref~              ~%missing_scroll_keys_0%~
                OUTER_TEXT_SPRINT ~bz_unleveled_scroll_base_caster_level~   ~%missing_scroll_keys_4%~
                OUTER_TEXT_SPRINT ~bz_unleveled_scroll_spell_level~         ~%missing_scroll_keys_3%~

                //get scroll cost
                LAUNCH_ACTION_FUNCTION ~fn-bz_calculate_scroll_cost~
                    INT_VAR
                        ~CasterLevel~ = ~%missing_scroll_keys_3%~
                        ~AdditionalCost~ = ~%missing_scroll_keys_5%~
                    STR_VAR
                        ~Level~ = ~%missing_scroll_keys_3%~
                        ~Tradition~ = ~%keys_2%~
                    RET
                        ~bz_unleveled_scroll_base_gold~ = ~gold~
                END
            END
        END
    END





    ACTION_IF NOT (~%bz_unleveled_scroll_resref%~ STRING_EQUAL ~~) THEN
    BEGIN
        ACTION_DEFINE_ASSOCIATIVE_ARRAY
            ~unleveled_scroll_list_data~
        BEGIN
            ~%keys_0%~,                                 //spell identifier
            ~%keys_1%~,                                 //tradition
            ~%bz_unleveled_scroll_spell_level%~,        //spell level
            ~%bz_unleveled_scroll_base_caster_level%~,  //caster level
            ~%bz_unleveled_scroll_base_gold%~,          //cost of the scroll in the *.ITM file
            ~%bz_unleveled_scroll_bam_prefix%~          //path/filname prefix for leveled BAMs
                => ~%keys_0%~           //a rather arbitrary value to pick. could be name, scroll, etc.
        END
    END
    ELSE BEGIN ACTION_IF (~%bz_unleveled_scroll_bam_prefix%~ STRING_EQUAL ~~) THEN BEGIN WARN ~WARN: leveled BAM group for %keys_0% not found~ END END


END




/* DEBUG */

PRINT ~DEBUG: unleveled_scroll_list_data:~
ACTION_PHP_EACH ~unleveled_scroll_list_data~
    AS ~keys~ => ~value~
BEGIN
    //Debugging Statements
    PRINT ~-Unleveled Scroll Metadata Record:
        Spell RESREF:               `%keys_0%`
        Spell Tradition:            `%keys_1%`
        Spell Level:                `%keys_2%`
        Spell Minimum Caster Level: `%keys_3%`
        Scroll minimum gold cost:   `%keys_4%`
        BAM Path/Prefix:            `%keys_5%`~
END
